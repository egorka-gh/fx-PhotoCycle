<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:barcode="com.photodispatcher.service.barcode.*"
					   xmlns:view="com.photodispatcher.view.*" 
					   xmlns:itemRenderer="com.photodispatcher.view.itemRenderer.*"
					   showStatusBar="false"
					   creationComplete="{initApp()}" 
					   closing="windowedapplication1_closingHandler(event)" 
					   >
	
	<fx:Script>
		<![CDATA[
			import com.photodispatcher.context.Context;
			import com.photodispatcher.event.BarCodeEvent;
			import com.photodispatcher.event.SerialProxyEvent;
			import com.photodispatcher.model.mysql.entities.SourceType;
			import com.photodispatcher.model.TechPoint;
			import com.photodispatcher.model.dao.BaseDAO;
			import com.photodispatcher.model.dao.TechPointDAO;
			import com.photodispatcher.model.dao.local.LocalDAO;
			import com.photodispatcher.model.mysql.entities.OrderState;
			import com.photodispatcher.service.barcode.ComInfo;
			import com.photodispatcher.service.barcode.ComReader;
			import com.photodispatcher.service.barcode.Socket2Com;
			import com.photodispatcher.service.barcode.ValveCom;
			import com.photodispatcher.util.ArrayUtil;
			import com.photodispatcher.util.StrUtil;
			import com.photodispatcher.view.config.DatabasePopup;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			
			import spark.events.IndexChangeEvent;
			import spark.events.PopUpEvent;
		
			[Bindable]
			private var techPionts:ArrayCollection;
			[Bindable]
			private var coms:ArrayCollection= new ArrayCollection;
			
			[Bindable]
			private var barcodeReader:ComReader;
			[Bindable]
			private var flap:ValveCom; 
			
			[Bindable]
			private var techPointObj:TechPoint;

			protected var _reversOrder:Boolean
			[Bindable]
			public function get reversOrder():Boolean{
				return _reversOrder;
			}
			public function set reversOrder(value:Boolean):void{
				_reversOrder = value;
				Context.setAttribute("reversOrder", value);
			}

			
			private var _techPoint:int;
			[Bindable]
			protected function get techPointID():int{
				return _techPoint;
			}
			protected function set techPointID(value:int):void{
				_techPoint = value;
				if(_techPoint){
					Context.setAttribute("techPoint", _techPoint);
					setTechPointObj(_techPoint);
				}
			}
			
			private function setTechPointObj(id:int):void{
				if(!techPionts) return;
				techPointObj=ArrayUtil.searchItem('id',id,techPionts.source) as TechPoint;
				Context.setAttribute("tech.object", techPointObj);
			}
			
			private var bdPopup:DatabasePopup;
			private function initApp():void{
				BaseDAO.MAX_WAITE=15000;
				bdPopup= new DatabasePopup();
				bdPopup.addEventListener(PopUpEvent.CLOSE,bdPopClose);
				bdPopup.chekConnect(SQLMode.UPDATE,false);
			}
			private function bdPopClose(e:PopUpEvent):void{
				var connected:Boolean=e.commit;
				if(connected){
					//create local connection
					connected=LocalDAO.connect();
				}
				if(connected){
					//connected
					init();
					//if(vMonitor) vMonitor.start();
				}else{
					//no connection
					currentState='disconnected';
					bdPopup.open(null);
				}
			}

			private function init():void{
				//TODO REFACTOR !!!
				OrderState.initStateMap();
				var dao:TechPointDAO=new TechPointDAO();
				/*
				var a:Array=dao.findByType(SourceType.TECH_PRINT);
				if(a){
					techPionts=new ArrayCollection(a);
				}
				*/
				techPionts=dao.findAll();
				coms.source=ComInfo.load();
				
				//var serverIP:String;
				var so:SharedObject = SharedObject.getLocal('appProps','/');
				if (so.size>0){
					/*
					serverIP=so.data.serverIP;
					*/
					techPointID=so.data.techPoint;
					reversOrder=so.data.reversOrder; 
				}

				/*
				Context.setAttribute('serverIP',serverIP);
				*/
				
				if(techPointObj && coms.length>0){
					start();
				}else{
					currentState='config';
				}
			}
			
			private function start():void{
				if(!techPointObj || coms.length==0){
					Alert.show('Приложение не настроено');
					currentState='config';
					return;
				}
				currentState='monitor';
				if(techMonitor) techMonitor.start();
				serialProxy.start(coms.source);
				if(serialProxy.isStarted){
					//start barcode reader
					if(barcodeReader){
						barcodeReader.stop();
						barcodeReader.removeEventListener(BarCodeEvent.BARCODE_ERR,barcodeReader_barcodeErrorHandler);
						barcodeReader=null;
					}
					var pr:Socket2Com=serialProxy.getProxy(ComInfo.COM_TYPE_BARREADER);
					if(pr){
						barcodeReader= new ComReader();
						barcodeReader.comPort=pr;
						barcodeReader.addEventListener(BarCodeEvent.BARCODE_ERR,barcodeReader_barcodeErrorHandler);
						barcodeReader.start();
					}
					pr=serialProxy.getProxy(ComInfo.COM_TYPE_VALVE);
					if(pr){
						flap= new ValveCom();
						flap.comPort=pr;
						flap.addEventListener(BarCodeEvent.BARCODE_ERR,barcodeReader_barcodeErrorHandler);
						flap.start();
					}
				}
			}

			private function stop():void{
				if(flap) flap.stop();
				if(techMonitor) techMonitor.stop();
				if(barcodeReader) barcodeReader.stop();
				serialProxy.stop();
				currentState='config';
			}

			protected function windowedapplication1_closingHandler(event:Event):void{
				//TODO flush data
				if(flap) flap.stop();
				if(barcodeReader) barcodeReader.stop();
				if(serialProxy) serialProxy.stop();
				if(techMonitor) techMonitor.stop();
			}
			
			protected function ddTechPoint_changeHandler(event:IndexChangeEvent):void{
				var tp:TechPoint= ddTechPoint.selectedItem as TechPoint;
				if (!tp) return;
				techPointID=tp.id;
				var so:SharedObject = SharedObject.getLocal('appProps','/');
				so.data.techPoint = techPointID;
				so.flush();  
			}
			
			protected function barcodeReader_barcodeErrorHandler(event:BarCodeEvent):void{
				if(flap) flap.stop();
				if(barcodeReader) barcodeReader.stop();
				serialProxy.stop();
				var bar:String=event.barcode;
				bar = bar.replace(String.fromCharCode(13),'[LF]');
				bar = bar.replace(String.fromCharCode(10),'[CR]');
				Alert.show('Ошибка \n'+event.error+';\n данные com: '+bar,'Ошибка');
				currentState='config';
			}
			
			protected function btAddCom_clickHandler(event:MouseEvent):void{
				var cp:ComInfo= new ComInfo();
				coms.addItem(cp);
				comList.selectedItem=cp;
			}
			
			protected function btSaveComs_clickHandler(event:MouseEvent):void{
				ComInfo.save(coms.source);
			}
			
			protected function serialProxy_serialProxyErrorHandler(event:SerialProxyEvent):void{
				Alert.show('Ошибка SerialProxy \n '+event.error,'Ошибка');
				currentState='config';
			}
			
			protected function chbRevers_changeHandler(event:Event):void{
				reversOrder=chbRevers.selected;
				var so:SharedObject = SharedObject.getLocal('appProps','/');
				so.data.reversOrder = reversOrder;
				so.flush();  
			}
			
			protected function btRecreateBD_clickHandler(event:MouseEvent):void{
				LocalDAO.recreateBD();
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<barcode:SerialProxy id="serialProxy" serialProxyError="serialProxy_serialProxyErrorHandler(event)"/>
	</fx:Declarations>

	<s:states>
		<s:State name="disconnected"/>
		<s:State name="config"/>
		<s:State name="monitor"/>
	</s:states>

	<s:Rect top="0" left="0" right="0" bottom="0">
		<s:fill>
			<s:SolidColor color="#cccccc"/>
		</s:fill>
	</s:Rect>

	<s:Button label="Выбрать БД" includeIn="disconnected,config" click="{bdPopup.open(null)}"
			  top="5" right="5"/>

	<s:VGroup  includeIn="config" gap="10" top="5" left="5" right="5" bottom="50">
		<s:HGroup gap="5" verticalAlign="baseline">
			<s:Label text="Технологическая точка:"/>
			<s:DropDownList id="ddTechPoint" dataProvider="{techPionts}" selectedItem="{techPointObj}" 
							width="250"
							labelField="name" change="ddTechPoint_changeHandler(event)"/>
		</s:HGroup>

		<s:HGroup gap="5" verticalAlign="baseline">
			<s:Label text="Обратный порядок:"/>
			<s:CheckBox id="chbRevers" selected="{reversOrder}"  change="chbRevers_changeHandler(event)"/>
		</s:HGroup>

		<s:VGroup gap="5">
			<s:HGroup gap="10">
				<s:List id="comList" dataProvider="{coms}" height="100%"/>
				<itemRenderer:ComInfoRenderer data="{comList.selectedItem}"/>
			</s:HGroup>
			<s:HGroup gap="10" width="100%" horizontalAlign="right">
				<s:Button id="btAddCom" label="Добавить" click="btAddCom_clickHandler(event)"/>
				<s:Button id="btSaveComs" label="Сохранить" click="btSaveComs_clickHandler(event)"/>
			</s:HGroup>
		</s:VGroup>
		
		<s:Spacer height="10"/>
		<s:Button id="btRecreateBD" label="Пересоздать локальную базу" click="btRecreateBD_clickHandler(event)"/>
	</s:VGroup>

	<s:VGroup  includeIn="monitor" gap="10" top="5" left="5" right="5" bottom="50">
		<view:TechMonitorView id="techMonitor" techPoint="{techPointObj}" barcodeReader="{barcodeReader}" flap="{flap}"
							  width="100%" height="100%"/>
	</s:VGroup>
	
	<s:HGroup bottom="5" width="100%" gap="10" paddingRight="10" paddingLeft="10" verticalAlign="middle">
		<s:Image source="{BaseDAO.isBusy?'assets/database_green.png':'assets/database_grey.png'}" width="24" height="24" toolTip="Запись в базу данных"/>
		<s:Group width="5">
			<s:Label text="{BaseDAO.lastErrMsg}" color="#cc0000"/>
		</s:Group>
		<s:Image source="{LocalDAO.isBusy?'assets/database_green.png':'assets/database_grey.png'}" width="24" height="24" toolTip="Запись в локальную базу данных"/>
		<s:Group width="5">
			<s:Label text="{LocalDAO.lastErrMsg}" color="#cc0000"/>
		</s:Group>
		<s:Spacer width="100%"/>
		<s:Button label="Стоп" includeIn="monitor" click="{stop()}"/>
		<s:Button label="Старт" includeIn="config" click="{start()}"/>
		<s:Button label="Закрыть" click="{FlexGlobals.topLevelApplication.exit()}" />
	</s:HGroup>

</s:WindowedApplication>

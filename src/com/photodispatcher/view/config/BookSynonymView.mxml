<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:config="com.photodispatcher.view.config.*"
		 creationComplete="{init()}">
	
<fx:Script>
	<![CDATA[
		import com.photodispatcher.context.Context;
		import com.photodispatcher.event.AsyncSQLEvent;
		import com.photodispatcher.model.mysql.entities.SourceType;
		import com.photodispatcher.model.dao.BaseDAO;
		import com.photodispatcher.model.mysql.DbLatch;
		import com.photodispatcher.model.mysql.entities.BookPgTemplate;
		import com.photodispatcher.model.mysql.entities.BookSynonym;
		import com.photodispatcher.model.mysql.services.BookSynonymService;
		import com.photodispatcher.util.ArrayUtil;
		
		import mx.collections.ArrayCollection;
		import mx.collections.ArrayList;
		import mx.collections.ListCollectionView;
		import mx.controls.Alert;
		
		import org.granite.tide.Tide;
		
		[Bindable]
		private var currentList:ArrayCollection=new ArrayCollection();
		[Bindable]
		private var bookTypeFilterList:ArrayCollection;

		[Bindable]
		private var currentBook:BookSynonym;
		[Bindable]
		private var templatesList:ArrayCollection=new ArrayCollection();
		
		private var service:BookSynonymService;

		private function refresh():void{
			//var dao:BookSynonymDAO=new BookSynonymDAO();
			//currentList.source=dao.findAllArray(SourceType.SRC_FOTOKNIGA);
			var latch:DbLatch= new DbLatch(false);
			latch.addEventListener(Event.COMPLETE, onLoad);
			latch.addLatch(service.loadAll(SourceType.SRC_FOTOKNIGA,0),'synonim');
			latch.start();
		}
		
		private function onLoad(event:Event):void{
			lockView=false;
			var latch:DbLatch= event.target as DbLatch;
			if(latch){
				latch.removeEventListener(Event.COMPLETE,onLoad);
				if(latch.complite){
					if(latch.lastToken.tag=='synonim'){
						currentList=latch.lastDataAC;
						currentList.filterFunction=gridFilter;
						currentList.refresh();
						currentState='list';
						//restore selection
						if(itemsGrid && lastSelectedBook){
							var selObj:Object;
							if(lastSelectedBook.id){
								selObj= ArrayUtil.searchItem('id',lastSelectedBook.id,currentList.source);
							}else if(lastSelectedBook.synonym){
								selObj= ArrayUtil.searchItem('synonym',lastSelectedBook.synonym,currentList.source);
							}
							if(selObj){
								itemsGrid.currentItem=selObj;
								itemsGrid.ensureSelectionVisible();
							}
							lastSelectedBook=null;
						}
					}else if(latch.lastToken.tag=='template'){
						if(latch.lastDataAC){
							templatesList=latch.lastDataAC;
						}else{
							templatesList= new ArrayCollection();
						}
						if (currentBook) currentBook.templates=templatesList;
						currentState='itemEdit';
					}else if(latch.lastToken.tag=='save'){
						refresh();
					}
				}else{
					currentList=null;
					templatesList=null;					
				}
			}
		}

		
		private function init():void{
			bookTypeFilterList=Context.getAttribute('book_typeList');
			service=Tide.getInstance().getContext().byType(BookSynonymService,true) as BookSynonymService;
			refresh();
		}

		private function addItem():void{
			if(currentState=='list'){
				var newPgs:BookSynonym= new BookSynonym();
				newPgs.src_type=SourceType.SRC_FOTOKNIGA;
				//fill vs filter vals
				if(tiSynonymFilter && tiSynonymFilter.text) newPgs.synonym=tiSynonymFilter.text;
				if(ddBookTypeFilter && ddBookTypeFilter.selectedIndex>0){
					newPgs.book_type=ddBookTypeFilter.selectedItem.value;
					newPgs.book_type_name=ddBookTypeFilter.selectedItem.label;
				}
				currentBook=newPgs;
				itemsGrid.addItem(newPgs);
			}else if(currentState=='itemEdit'){
				var newTemp:BookPgTemplate=new BookPgTemplate();
				newTemp.book=currentBook.id;
				tempGrid.addItem(newTemp);
			}
		}

		[Bindable]
		private var lockView:Boolean=false;
		private var lastSelectedBook:Object;
		private function saveAll():void{
			var item:BookSynonym;
			var temp:BookPgTemplate;
			var itemsToSave:ArrayCollection= new ArrayCollection(); //:ListCollectionView= new ListCollectionView(new ArrayList());
			for each (item in currentList){
				if(item){
					if(!item.loaded || item.changed){
						itemsToSave.addItem(item);
					}else if(item.templates){
						for each(temp in item.templates){
							if(temp && (!temp.loaded || temp.changed)){
								itemsToSave.addItem(item);
								break;
							}
						}
					}
				}
			}
			if(itemsToSave.length!=0){
				if(!currentBook && itemsGrid && itemsGrid.currentItem) currentBook=itemsGrid.currentItem as BookSynonym;
				if(currentBook){
					lastSelectedBook= new Object();
					if(currentBook.loaded) lastSelectedBook.id=currentBook.id;
					lastSelectedBook.synonym=currentBook.synonym;
				}
				lockView=true;
				var latch:DbLatch= new DbLatch(false);
				latch.addEventListener(Event.COMPLETE, onLoad);
				latch.addLatch(service.persistBatch(itemsToSave),'save');
				latch.start();
			}
		}

		private function applyFilter():void{
			currentList.refresh();
		}

		private function gridFilter(item:Object):Boolean{
			var synonym:BookSynonym=item as BookSynonym;
			if(!synonym) return false;
			if(tiSynonymFilter && tiSynonymFilter.text && synonym.synonym.substr(0,tiSynonymFilter.text.length)!=tiSynonymFilter.text) return false;
			if(ddBookTypeFilter && ddBookTypeFilter.selectedIndex>0 && synonym.book_type!=ddBookTypeFilter.selectedItem.value) return false;
			return true;
		}

		protected function btEdit_clickHandler(event:MouseEvent):void{
			currentBook = itemsGrid.currentItem as BookSynonym;
			//templatesList.source=null;
			if(currentBook){
				if(!currentBook.loaded){
					Alert.show('Необходимо сохранить новую запись.');
					return;
				}
				/*
				if (!currentBook.templates){
					if(!currentBook.loadTemplates()) return;
				}
				if (!currentBook.templates) currentBook.templates=[];
				templatesList.source=currentBook.templates;
				currentState='itemEdit';
				*/
				var latch:DbLatch= new DbLatch(false);
				latch.addEventListener(Event.COMPLETE, onLoad);
				latch.addLatch(service.loadTemplates(currentBook.id),'template');
				latch.start();
			}
		}
		
		protected function btTech_clickHandler(event:MouseEvent):void{
			if(!tempGrid || !tempGrid.currentItem) return;
			var templ:BookPgTemplate= tempGrid.currentItem as BookPgTemplate;
			if(!templ) return;
			var pop:TechConfigPopup= new TechConfigPopup();
			pop.show(templ);
		}
		
	]]>
</fx:Script>
	<s:states>
		<s:State name="list"/>
		<s:State name="itemEdit"/>
	</s:states>
	
	<s:VGroup gap="10" width="100%" height="100%">
		<s:HGroup id="gFilters" width="100%" gap="5" horizontalAlign="left" verticalAlign="baseline" includeIn="list">
			<s:Label text="Фильтрация" fontWeight="bold"/>

			<s:Spacer width="5"/>
			<s:Label text="Имя папки"/>
			<s:TextInput id="tiSynonymFilter" width="300" change="{applyFilter()}"/>
			
			<s:Spacer width="5"/>
			<s:Label text="Тип книги"/>
			<s:DropDownList id="ddBookTypeFilter" dataProvider="{bookTypeFilterList}" change="{applyFilter()}"/>
			
			<s:Spacer width="100%"/>
			<s:Button id="btRefresh" icon="assets/icon-refresh.gif" click="{refresh()}"
					  cornerRadius="4" width="22" height="22" baseline="4"/>
		</s:HGroup>
		<config:ItemsGrid id="itemsGrid" editable="true" includeIn="list"
						  currentData="{currentList}" currentColumns="{BookSynonym.gridColumns()}" 
						  width="100%" height="100%"/>
		<s:Label includeIn="itemEdit" text="{currentBook.synonym}" width="100%" fontWeight="bold"/>
		<s:Label includeIn="itemEdit" text="{currentBook.book_type_name}" width="100%"/>
		<config:ItemsGrid id="tempGrid" editable="true" includeIn="itemEdit"
						  currentData="{templatesList}" currentColumns="{BookPgTemplate.gridColumns()}" 
						  width="100%" height="100%"/>
		<s:HGroup gap="10" verticalAlign="baseline" width="100%" horizontalAlign="left">
			<!--TODO implement DELETE-->
			<s:Button id="btAdd" label="Добавить" click="{addItem()}"/>
			<s:Button id="btEdit" enabled="true" enabled.itemEdit="false" label="Состав книги" click="btEdit_clickHandler(event)"/>
			<s:Button id="btSave" label="Сохранить" click="{saveAll()}"/>
			<s:Button id="btCancel" label="Отмена" click="{currentState='list';refresh();}"/>
			<s:Spacer width="50"/>
			<s:Button id="btTech" label="Технологическая разметка" includeIn="itemEdit" click="btTech_clickHandler(event)"/>
		</s:HGroup>
	</s:VGroup>
</s:Group>

<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 width="100%" height="100%" 
		 creationComplete="{init()}">
	<fx:Script>
		<![CDATA[
			import com.photodispatcher.context.Context;
			import com.photodispatcher.event.PrintEvent;
			import com.photodispatcher.factory.LabBuilder;
			import com.photodispatcher.model.OrderState;
			import com.photodispatcher.model.PrintGroup;
			import com.photodispatcher.model.Roll;
			import com.photodispatcher.model.Source;
			import com.photodispatcher.model.dao.BaseDAO;
			import com.photodispatcher.model.dao.OrderStateDAO;
			import com.photodispatcher.model.dao.PrintGroupDAO;
			import com.photodispatcher.model.dao.SourcesDAO;
			import com.photodispatcher.print.LabBase;
			import com.photodispatcher.print.PrintQueueManager;
			import com.photodispatcher.view.menu.GridContexMenu;
			
			import mx.collections.ArrayCollection;
			import mx.collections.IList;
			import mx.events.FlexEvent;
			
			import spark.events.IndexChangeEvent;
			[Bindable]
			private var ordTotal:int;
			[Bindable]
			private var pgTotal:int;
			[Bindable]
			private var phTotal:int;
			
			[Bindable]
			private var pgList:ArrayCollection=new ArrayCollection();
			private var pgArray:Array;
			
			[Bindable]
			private var printManager:PrintQueueManager;

			//filters
			[Bindable]
			private var sizesList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var sourceFilterList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var sizesFilterList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var paperFilterList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var statesFilterList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var booleanFilterList:ArrayCollection= new ArrayCollection();

			private var gridMenu:GridContexMenu;

			private function init():void{
				refresh();
				
				var a:Array=Context.getSources();
				if(!a) a=[];
				var s:Source=new Source(); s.id=0; s.name='-';
				a.unshift(s);
				sourceFilterList.source=a;
				
				a=OrderStateDAO.getStateArray(OrderState.PRN_WAITE,OrderState.PRN_CANCEL+1,true);
				var os:OrderState=new OrderState(); os.id=0; os.name='-';
				a.unshift(os);
				statesFilterList.source=a;

				pgList.filterFunction=filterPGList;
				pgList.refresh();
				Context.initAttributeLists();
				booleanFilterList=Context.getAttribute('booleanList');

			}
			
			public function refresh(afterDistribute:Boolean=false):void{
				if(!printManager){
					printManager= PrintQueueManager.instance;
					printManager.addEventListener(PrintEvent.MANAGER_ERROR_EVENT, onManagerError);
				}
				printManager.init();
				refreshPgList();
				//refresh lab list
				if(afterDistribute) return;
			}

			private function onManagerError(e:PrintEvent):void{
				lbManagerError.text=e.errMsg;
			}
			
			private function refreshPgList():void{
				var pgDao:PrintGroupDAO=new PrintGroupDAO();
				var a:Array=pgDao.findAllArray(OrderState.PRN_WAITE,OrderState.PRN_PRINT);
				if(!a) return;//read lock
				if(printManager) printManager.reSync(a);
				pgArray=a;
				//reset filter
				if(lstSizes){
					lstSizes.selectedIndex=-1;
					lstSizes_changeHandler(null);
				}else{
					pgList.source=pgArray;
				}
				calcTotals(a);
			}
			
			/*
			private function calcTotals(pgArray:Array):void{
				ordTotal=0;
				pgTotal=0;
				phTotal=0;
				if(!pgArray){
					sizesList.source=[];
					return;
				}
				var pg:PrintGroup; 
				var orderMap:Object=new Object();
				var o:Object;
				var sizeMap:Object= new Object();
				var sizeFilterMap:Object= new Object();
				var paperFilterMap:Object= new Object();
				var key:String;
				var size:PhotoSizeAccum;
				for each(o in pgArray){
					pg= o as PrintGroup;
					orderMap[pg.order_id]=pg.order_id;
					//total printgroups
					pgTotal++;
					//total files
					phTotal+=pg.file_num;
					//fill sizesList 
					key=pg.width.toString()+'~'+pg.paper.toString();
					size=sizeMap[key] as PhotoSizeAccum;
					if(!size){
						size= new PhotoSizeAccum();
						size.size=pg.width;
						size.paper_id=pg.paper;
						size.paper=pg.paper_name;
						sizeMap[key]=size;
					}
					size.pg_num++;
					size.file_num+=pg.file_num;
					size.len+=pg.file_num*pg.height;
					//fill size filter list
					sizeFilterMap[size.size.toString()]={id:size.size, name:size.size.toString()};
					//fill paper filter list
					paperFilterMap[size.paper_id.toString()]={id:size.paper_id, name:size.paper};
					if(pg.width!=pg.height){
						sizeFilterMap[pg.height.toString()]={id:pg.height, name:pg.height.toString()};
					}
				}
				//total orders
				for (o in orderMap) ordTotal++;
				//sizesList
				var a:Array=[];
				for each (o in sizeMap) a.push(o);
				a.sortOn('file_num',Array.DESCENDING | Array.NUMERIC);
				size=new PhotoSizeAccum(); size.size=-1;
				a.unshift(size);
				sizesList.source=a;
				//sizesFilterList
				a=[];
				for each (o in sizeFilterMap) a.push(o);
				a.sortOn('id',Array.NUMERIC);
				a.unshift({id:-1,name:'-'});
				sizesFilterList.source=a;
				//paper FilterList
				a=[];
				for each (o in paperFilterMap){
					if(o.id) a.push(o);
				}
				a.sortOn('id',Array.NUMERIC);
				a.unshift({id:-1,name:'-'});
				paperFilterList.source=a;
			}
			*/
			
			private function calcTotals(pgArray:Array):void{
				if(!pgArray){
					//sizesList.source=[];
					return;
				}
				ordTotal=0;
				pgTotal=0;
				phTotal=0;
				var standartSize:Object=Roll.itemsMap();
				var pg:PrintGroup; 
				var orderMap:Object=new Object();
				var sizeMap:Object= new Object();
				var sizeFilterMap:Object= new Object();
				var paperFilterMap:Object= new Object();
				var key:String;
				var size:PhotoSizeAccum;
				
				for each(pg in pgArray){
					orderMap[pg.order_id]=pg.order_id;
					//total printgroups
					pgTotal++;
					//total files
					phTotal+=pg.prints?pg.prints:pg.file_num;

					//fill paper filter list
					paperFilterMap[pg.paper.toString()]={id:pg.paper, name:pg.paper_name};

					//fill sizesList
					key='';
					//by width
					//check if standart
					if(standartSize[pg.width.toString()]){
						key=pg.width.toString()+'~'+pg.paper.toString();
						size=sizeMap[key] as PhotoSizeAccum;
						if(!size){
							size= new PhotoSizeAccum();
							size.size=pg.width;
							size.paper_id=pg.paper;
							size.paper=pg.paper_name;
							sizeMap[key]=size;
						}
						size.pg_num++;
						size.file_num+=pg.prints?pg.prints:pg.file_num;
						size.len+=(pg.prints?pg.prints:pg.file_num)*pg.height;
						size.items.push(pg);
						//fill size filter list
						sizeFilterMap[size.size.toString()]={id:size.size, name:size.caption};
					}
					
					//by height
					if(pg.width!=pg.height){
						if(standartSize[pg.height.toString()]){
							key=pg.height.toString()+'~'+pg.paper.toString();
							size=sizeMap[key] as PhotoSizeAccum;
							if(!size){
								size= new PhotoSizeAccum();
								size.size=pg.height;
								size.paper_id=pg.paper;
								size.paper=pg.paper_name;
								sizeMap[key]=size;
							}
							size.pg_num++;
							size.file_num+=pg.prints?pg.prints:pg.file_num;
							size.len+=(pg.prints?pg.prints:pg.file_num)*pg.width;
							size.items.push(pg);
							//fill size filter list
							sizeFilterMap[size.size.toString()]={id:size.size, name:size.caption};
						}
					}
					//not standart
					if(!key){
						key='0~0';
						size=sizeMap[key] as PhotoSizeAccum;
						if(!size){
							size= new PhotoSizeAccum();
							size.caption='Не стандарт';
							sizeMap[key]=size;
						}
						size.pg_num++;
						size.file_num+=pg.prints?pg.prints:pg.file_num;
						size.items.push(pg);
						//fill size filter list
						sizeFilterMap[pg.width.toString()]={id:pg.width, name:pg.width.toString()};
						if(pg.width!=pg.height) sizeFilterMap[pg.height.toString()]={id:pg.height, name:pg.height.toString()};
					}
				}
				//total orders
				var o:Object;
				for (o in orderMap) ordTotal++;
				//sizesList
				var a:Array=[];
				for each (o in sizeMap) a.push(o);
				a.sortOn('len',Array.DESCENDING | Array.NUMERIC);
				size=new PhotoSizeAccum(); size.size=-1; size.caption='-';
				a.unshift(size);
				sizesList.source=a;
				//sizesFilterList
				a=[];
				for each (o in sizeFilterMap) a.push(o);
				a.sortOn('id',Array.NUMERIC);
				a.unshift({id:-1,name:'-'});
				sizesFilterList.source=a;
				//paper FilterList
				a=[];
				for each (o in paperFilterMap){
					if(o.id) a.push(o);
				}
				a.sortOn('id',Array.NUMERIC);
				a.unshift({id:-1,name:'-'});
				paperFilterList.source=a;
			}
			
			private function sizesListLabel(item:Object):String{
				if(item.size==-1){
					return '-';
				}else if(item.size==0){
					return item.caption+' (кол: '+item.file_num+')';
				}else{
					return item.caption+' '+item.paper+' (кол: '+item.file_num+'; длина: '+ Math.round(item.len/1000).toString()+'м)';
				}
			}
			
			protected function btPost_clickHandler(event:MouseEvent):void{
				if(!dgPGroups.selectedItem || !lstLabs.selectedItem) return;
				lbManagerError.text='';
				var l:LabBase=lstLabs.selectedItem as LabBase;
				printManager.post(dgPGroups.selectedItems,l);
			}
			
			private function filterPGList(item:Object):Boolean {
				var pg:PrintGroup=item as PrintGroup;
				if(!pg) return false;
				if(ddSourceFilter && ddSourceFilter.selectedIndex>0 && pg.source_id!=ddSourceFilter.selectedItem.id){
					return false;
				}
				if(tiOrderFilter && tiOrderFilter.text && pg.id.indexOf(tiOrderFilter.text)==-1) return false;
				if(ddStateFilter && ddStateFilter.selectedIndex>0 && pg.state!=ddStateFilter.selectedItem.id) return false;
				if(ddIsBookFilter && ddIsBookFilter.selectedIndex>0){
					if(ddIsBookFilter.selectedItem.value && pg.book_type==0) return false;
					if(!ddIsBookFilter.selectedItem.value && pg.book_type!=0) return false;
				}
				if(ddIsPdfFilter && ddIsPdfFilter.selectedIndex>0 && ddIsPdfFilter.selectedItem.value != pg.is_pdf) return false;
				/*
				if(lstSizes && lstSizes.selectedIndex>0){
					var ps:PhotoSizeAccum=lstSizes.selectedItem as PhotoSizeAccum;
					if((pg.width!=ps.size) ||(pg.paper!=ps.paper_id)) return false;
				}else{
					if(ddSizeFilter && ddSizeFilter.selectedIndex>0){
						if(pg.width!=ddSizeFilter.selectedItem.id && pg.height!=ddSizeFilter.selectedItem.id) return false;
					}
					if(ddPaperFilter && ddPaperFilter.selectedIndex>0 && pg.paper!=ddPaperFilter.selectedItem.id) return false;
				}
				*/
				if(!lstSizes || lstSizes.selectedIndex<=0){
					if(ddSizeFilter && ddSizeFilter.selectedIndex>0){
						if(pg.width!=ddSizeFilter.selectedItem.id && pg.height!=ddSizeFilter.selectedItem.id) return false;
					}
					if(ddPaperFilter && ddPaperFilter.selectedIndex>0 && pg.paper!=ddPaperFilter.selectedItem.id) return false;
				}
				return true;
			}
			
			private function applyFilter():void{
				pgList.refresh();
				if(dgPGroups){
					dgPGroups.clearSelection();
					dgPGroups.dispatchEvent(new FlexEvent(FlexEvent.VALUE_COMMIT));
				}
			}
			
			protected function lstSizes_changeHandler(event:IndexChangeEvent):void{
				var enabled:Boolean=lstSizes.selectedIndex<1;
				//ddStateFilter.enabled=enabled;
				ddSizeFilter.enabled=enabled;
				ddPaperFilter.enabled=enabled;
				var sz:PhotoSizeAccum;
				if(lstSizes.selectedIndex>0){
					sz=lstSizes.selectedItem as PhotoSizeAccum;
				}
				if(sz){
					pgList.source=sz.items;
				}else{
					pgList.source=pgArray;
				}
				
				applyFilter();				
			}

			protected function dgPGroups_creationCompleteHandler(event:FlexEvent):void{
				gridMenu= new GridContexMenu(dgPGroups,[GridContexMenu.CANCEL_ORDER,GridContexMenu.SEPARATOR,GridContexMenu.SHOW_ORDER]);
			}
			
			protected function btRefreshLab_clickHandler(event:MouseEvent):void{
				printManager.refreshLabs();
			}
			
		]]>
	</fx:Script>
	<s:HGroup gap="10" width="100%" height="100%">
		<s:VGroup gap="5" width="230" height="100%">
			<s:Label text="Заказов - {ordTotal}" left="0" verticalCenter="0"/>
			<s:Label text="Групп печати - {pgTotal}"/>
			<s:Label text="Кол. отпечатков - {phTotal}"/>
			<s:Spacer height="5"/>
			<s:Label text="Варианты магазинов"/>
			<s:List id="lstSizes" width="100%" height="100%"
					labelFunction="sizesListLabel" dataProvider="{sizesList}"
					change="lstSizes_changeHandler(event)"/>
		</s:VGroup>
		<s:VGroup gap="5" width="100%" height="100%">
			<s:Label id="lbManagerError" color="#ff0000" width="100%" maxDisplayedLines="1"/>
			<s:HGroup id="gFilters" width="100%" gap="5" horizontalAlign="left" verticalAlign="baseline">
				<s:Label text="Фильтрация" fontWeight="bold"/>
				
				<s:Spacer width="5"/>
				<s:Label text="Источник"/>
				<s:DropDownList id="ddSourceFilter" dataProvider="{sourceFilterList}" labelField="name" change="{applyFilter()}" width="100"/>
				
				<s:Spacer width="5"/>
				<s:Label text="ID"/>
				<s:TextInput id="tiOrderFilter" width="80" change="{applyFilter()}"/>
				
				<s:Spacer width="5"/>
				<s:Label text="Статус"/>
				<s:DropDownList id="ddStateFilter" dataProvider="{statesFilterList}" width="150" labelField="name" change="{applyFilter()}"/>
				
				<s:Spacer width="5"/>
				<s:Label text="Размер"/>
				<s:DropDownList id="ddSizeFilter" dataProvider="{sizesFilterList}" labelField="name" change="{applyFilter()}"  width="60"/>
				
				<s:Spacer width="5"/>
				<s:Label text="Бумага"/>
				<s:DropDownList id="ddPaperFilter" dataProvider="{paperFilterList}" labelField="name" change="{applyFilter()}" width="100"/>

				<s:Spacer width="5"/>
				<s:Label text="Книга"/>
				<s:DropDownList id="ddIsBookFilter" dataProvider="{booleanFilterList}" change="{applyFilter()}" width="60"/>

				<s:Spacer width="5"/>
				<s:Label text="PDF"/>
				<s:DropDownList id="ddIsPdfFilter" dataProvider="{booleanFilterList}" change="{applyFilter()}" width="60"/>
				
				<s:Spacer width="100%"/>
				<s:Button id="btRefresh" icon="assets/icon-refresh.gif" click="{refreshPgList()}"
						  cornerRadius="4" width="22" height="22" baseline="4"/>
			</s:HGroup>
			<s:DataGrid id="dgPGroups" selectionMode="multipleRows"
						width="100%" height="100%" creationComplete="dgPGroups_creationCompleteHandler(event)"
						dataProvider="{pgList}" locale="ru_RU" columns="{PrintGroupDAO.gridColumns()}"/>
		</s:VGroup>
		<s:Group height="100%">
			<s:Button id="btPost" label="{printManager.isBusy?'!':'>'}" width="30" fontWeight="bold" verticalCenter="0" 
					  enabled="{printManager.isBusy?false:true}" click="btPost_clickHandler(event)"/>
		</s:Group>
		<s:VGroup gap="5" width="200" height="100%">
			<s:HGroup gap="10" verticalAlign="middle" width="100%">
				<s:Label text="Лаборатории"/>
				<s:Spacer width="100%"/>
				<s:Button id="btRefreshLab" icon="assets/icon-refresh.gif" click="btRefreshLab_clickHandler(event)"
						  cornerRadius="4" width="22" height="22" baseline="4"/>

			</s:HGroup>
			<s:List id="lstLabs" width="100%" height="100%"
					dataProvider="{printManager.labs}" useVirtualLayout="false"
					itemRenderer="com.photodispatcher.view.itemRenderer.LabItemRenderer"/>
		</s:VGroup>
	</s:HGroup>
</s:Group>

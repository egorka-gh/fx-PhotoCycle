<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 creationComplete="{init()}">
	
	<fx:Script>
		<![CDATA[
			import com.photodispatcher.context.Context;
			import com.photodispatcher.event.PrintEvent;
			import com.photodispatcher.factory.LabBuilder;
			import com.photodispatcher.model.Lab;
			import com.photodispatcher.model.OrderState;
			import com.photodispatcher.model.PrintGroup;
			import com.photodispatcher.model.Source;
			import com.photodispatcher.model.dao.LabDAO;
			import com.photodispatcher.model.dao.OrderStateDAO;
			import com.photodispatcher.model.dao.PrintGroupDAO;
			import com.photodispatcher.model.dao.SourcesDAO;
			import com.photodispatcher.print.LabBase;
			import com.photodispatcher.print.PrintQueueManager;
			import com.photodispatcher.util.ArrayUtil;
			import com.photodispatcher.view.menu.GridContexMenu;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			
			import spark.events.IndexChangeEvent;
			
			[Bindable]
			private var pgList:ArrayCollection=new ArrayCollection();
			
			//filters
			[Bindable]
			private var labFilterList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var sizesList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var sourceFilterList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var paperFilterList:ArrayCollection= new ArrayCollection();
			[Bindable]
			private var statesFilterList:ArrayCollection= new ArrayCollection();
			
			private var gridMenu:GridContexMenu;

			//private var labId:int;
			//private var lab:LabBase;
			private var labMap:Object;

			[Bindable]
			private var ordTotal:int;
			[Bindable]
			private var pgTotal:int;
			[Bindable]
			private var phTotal:int;

			private var printManager:PrintQueueManager;

			public function init():void{
				//labId=Context.getAttribute('applabId');
				
				labMap=PrintQueueManager.instance.labMap;

				var lDao:SourcesDAO= new SourcesDAO();
				var a:Array=lDao.findAllArray();
				if(!a) a=[];
				var s:Source=new Source(); s.id=0; s.name='-';
				a.unshift(s);
				sourceFilterList.source=a;

				var labDao:LabDAO= new LabDAO();
				a=labDao.findAllArray();
				if(!a) a=[];
				var lab:Lab= new Lab(); lab.id=0; lab.name='-';
				a.unshift(lab);
				labFilterList.source=a;

				Context.initAttributeLists();
				paperFilterList=Context.getAttribute('paperList');

				a=OrderStateDAO.getStateArray(OrderState.WAITE_FTP,OrderState.PRN_COMPLETE+1,true);
				var os:OrderState=new OrderState(); os.id=0; os.name='-';
				a.unshift(os);
				statesFilterList.source=a;

				pgList.filterFunction=filterPGList;
				
				refresh();
			}

			public function refresh():void{
				if(!printManager){
					printManager=PrintQueueManager.instance;
					printManager.addEventListener(PrintEvent.MANAGER_ERROR_EVENT, onManagerError);
				}
				//printManager.init();
				var pgDao:PrintGroupDAO=new PrintGroupDAO();
				var a:Array=pgDao.findAllInPrint(); 
				if(a){
					//TODO can lost unsaved states (in write queue)
					pgList.source=a;
				}
				calcTotals(a);
			}
			
			private function onManagerError(e:PrintEvent):void{
				lbManagerError.text=e.errMsg;
			}

			private function calcTotals(pintGroups:Array):void{
				var pgArray:Array=pintGroups.filter(totalsFilter);
				ordTotal=0;
				pgTotal=pgArray.length;
				phTotal=0;
				if(!pgArray){
					sizesList.source=[];
					return;
				}
				var pg:PrintGroup; 
				var orderMap:Object=new Object();
				var o:Object;
				var sizeMap:Object= new Object();
				var key:String;
				var size:PhotoSizeAccum;
				for each(o in pgArray){
					pg= o as PrintGroup;
					orderMap[pg.order_id]=pg.order_id;
					//total files
					phTotal+=pg.file_num;
					//fill sizesList 
					key=pg.width.toString()+'~'+pg.paper.toString();
					size=sizeMap[key] as PhotoSizeAccum;
					if(!size){
						size= new PhotoSizeAccum();
						size.size=pg.width;
						size.paper_id=pg.paper;
						size.paper=pg.paper_name;
						sizeMap[key]=size;
					}
					size.pg_num++;
					size.file_num+=pg.file_num;
					size.len+=pg.file_num*pg.height;
					/*by height
					if(pg.width!=pg.height){
						key=pg.height.toString()+'~'+pg.paper.toString();
						size=sizeMap[key] as PhotoSizeAccum;
						if(!size){
							size= new PhotoSizeAccum();
							size.size=pg.height;
							size.paper_id=pg.paper;
							size.paper=pg.paper_name;
							sizeMap[key]=size;
						}
						size.pg_num++;
						size.file_num+=pg.file_num;
						size.len+=pg.file_num*pg.width;
					}
					*/
				}
				//total orders
				for (o in orderMap) ordTotal++;
				//sizesList
				var a:Array=[];
				for each (o in sizeMap) a.push(o);
				a.sortOn('file_num',Array.DESCENDING | Array.NUMERIC);
				size=new PhotoSizeAccum(); size.size=-1;
				a.unshift(size);
				sizesList.source=a;
			}
			protected function totalsFilter(element:*, index:int, arr:Array):Boolean {
				var o:PrintGroup=element as PrintGroup;
				//return o!=null && o.state==syncState;
				//return o!=null && o.destination==labId && o.state==OrderState.PRN_PRINT;
				return o!=null && labMap && labMap[o.destination.toString()] && o.state==OrderState.PRN_PRINT;
			}

			private function filterPGList(item:Object):Boolean {
				var pg:PrintGroup=item as PrintGroup;
				if(!pg) return false;

				if(ddSourceFilter && ddSourceFilter.selectedIndex>0 && pg.source_id!=ddSourceFilter.selectedItem.id) return false;
				if(tiOrderFilter && tiOrderFilter.text && pg.id.indexOf(tiOrderFilter.text)==-1) return false;
				if(lstSizes && lstSizes.selectedIndex>0){
					var ps:PhotoSizeAccum=lstSizes.selectedItem as PhotoSizeAccum;
					//if(pg.destination!=labId || pg.state!=OrderState.PRN_PRINT || (pg.width!=ps.size && pg.height!=ps.size) ||(pg.paper!=ps.paper_id)) return false; 
					//if(pg.destination!=labId || pg.state!=OrderState.PRN_PRINT || (pg.width!=ps.size) ||(pg.paper!=ps.paper_id)) return false;
					if(!labMap || !labMap[pg.destination.toString()] || pg.state!=OrderState.PRN_PRINT || (pg.width!=ps.size) ||(pg.paper!=ps.paper_id)) return false;
				}else{
					if(ddLabFilter && ddLabFilter.selectedIndex>0 && pg.destination!=ddLabFilter.selectedItem.id) return false;
					if(ddStateFilter && ddStateFilter.selectedIndex>0 && pg.state!=ddStateFilter.selectedItem.id) return false;
					if(tiSizeFilter && tiSizeFilter.text){
						if(pg.width!=int(tiSizeFilter.text) && pg.height!=int(tiSizeFilter.text)) return false;
					}
					if(ddPaperFilter && ddPaperFilter.selectedIndex>0 && pg.paper!=ddPaperFilter.selectedItem.value) return false;
				}
				return true;
			}

			private function applyFilter():void{
				pgList.refresh();
				if(dgPGroups){
					dgPGroups.clearSelection();
					dgPGroups.dispatchEvent(new FlexEvent(FlexEvent.VALUE_COMMIT));
				}
			}

			private function sizesListLabel(item:Object):String{
				if(item.size!=-1){
					return item.size+' '+item.paper+' (фото: '+item.file_num+'; длина: '+ Math.round(item.len/1000).toString()+'м)';
				}else{
					return '-';
				}
			}

			protected function dgPGroups_creationCompleteHandler(event:FlexEvent):void{
				gridMenu= new GridContexMenu(dgPGroups,[GridContexMenu.SHOW_ORDER]);
				gridMenu.addItem('Отменить печать',cancelPrintCallBack);
				gridMenu.addItem('Напечатан',setPrintedCallBack);
			}
			
			
			private function cancelPrintCallBack(grid:DataGrid,param:int):void{
				//if(!lab) return;
				if(!labMap) return;
				if(!printManager) return;
				if (printManager.isBusy){
					Alert.show('Не завершена предыдущая операция. Повторите отмену позже.');
					return;
				}
				var a:Array=[];
				var pg:PrintGroup;
				for each(var o:Object in grid.selectedItems){
					pg= o as PrintGroup;
					//if(pg && pg.state==OrderState.PRN_PRINT && pg.destination==labId){
					if(pg && pg.state==OrderState.PRN_PRINT && labMap[pg.destination.toString()]){
						a.push(pg);
					}
				}
				if(a.length>0){
					printManager.cancelPost(a);
				}
			}
			
			private function setPrintedCallBack(grid:DataGrid,param:int):void{
				//if(!lab) return;
				if(!labMap) return;
				if(!printManager) return;
				var a:Array=[];
				var pg:PrintGroup;
				for each(var o:Object in grid.selectedItems){
					pg= o as PrintGroup;
					if(pg && pg.state==OrderState.PRN_PRINT && labMap[pg.destination.toString()]){
						pg.state=OrderState.PRN_COMPLETE;
						a.push(pg);
					}
				}
				if(a.length>0){
					//printManager.cancelPost(a,lab);
					printManager.savePrintState(a);
				}
			}

			protected function lstSizes_changeHandler(event:IndexChangeEvent):void{
				var enabled:Boolean=lstSizes.selectedIndex<1;
				ddLabFilter.enabled=enabled;
				ddStateFilter.enabled=enabled;
				tiSizeFilter.enabled=enabled;
				ddPaperFilter.enabled=enabled;
				
				applyFilter();				
			}
			
		]]>
	</fx:Script>
	
	<s:HGroup gap="10" width="100%" height="100%">
		<s:VGroup gap="5" width="250" height="100%">
			<s:Label text="Заказов - {ordTotal}" left="0" verticalCenter="0"/>
			<s:Label text="Групп печати - {pgTotal}"/>
			<s:Label text="Фото - {phTotal}"/>
			<s:Spacer height="5"/>
			<s:Label text="Варианты ширины"/>
			<s:List id="lstSizes" width="100%" height="100%"
					labelFunction="sizesListLabel" dataProvider="{sizesList}"
					change="lstSizes_changeHandler(event)"/>
		</s:VGroup>
		<s:VGroup gap="5" width="100%" height="100%">
			<s:Label id="lbManagerError" color="#ff0000" width="100%" maxDisplayedLines="1"/>
			<s:HGroup id="gFilters" width="100%" gap="5" horizontalAlign="left" verticalAlign="baseline">
				<s:Label text="Фильтрация" fontWeight="bold"/>
				
				<s:Spacer width="5"/>
				<s:Label text="Лаборатория"/>
				<s:DropDownList id="ddLabFilter" dataProvider="{labFilterList}" labelField="name" change="{applyFilter()}" width="100"/>
				
				<s:Spacer width="5"/>
				<s:Label text="Источник"/>
				<s:DropDownList id="ddSourceFilter" dataProvider="{sourceFilterList}" labelField="name" change="{applyFilter()}" width="100"/>
				
				<s:Spacer width="5"/>
				<s:Label text="ID"/>
				<s:TextInput id="tiOrderFilter" width="80" change="{applyFilter()}"/>
				
				<s:Spacer width="5"/>
				<s:Label text="Статус"/>
				<s:DropDownList id="ddStateFilter" dataProvider="{statesFilterList}" width="150" labelField="name" change="{applyFilter()}"/>

				<s:Spacer width="5"/>
				<s:Label text="Размер"/>
				<!--<s:DropDownList id="ddSizeFilter" dataProvider="{sizesFilterList}" labelField="name" change="{applyFilter()}"/>-->
				<s:TextInput id="tiSizeFilter" restrict="0123456789" width="60" change="{applyFilter()}"/>
				
				<s:Spacer width="5"/>
				<s:Label text="Бумага"/>
				<s:DropDownList id="ddPaperFilter" dataProvider="{paperFilterList}" change="{applyFilter()}" width="100"/>
				
				<s:Spacer width="100%"/>
				<s:Button id="btRefresh" icon="assets/icon-refresh.gif" click="{refresh()}"
						  cornerRadius="4" width="22" height="22" baseline="4"/>
			</s:HGroup>
			<s:DataGrid id="dgPGroups" selectionMode="multipleRows"
						width="100%" height="100%" creationComplete="dgPGroups_creationCompleteHandler(event)"
						dataProvider="{pgList}" locale="ru_RU" columns="{PrintGroupDAO.gridColumns(true)}"/>
		</s:VGroup>
	</s:HGroup>
	
</s:Group>
